# Defines a few macros, variables and a function to help with compiling Rust
# programs/libraries and documentation with CMake.
#
# This file utilizes several global variables:
# 
# RUSTC_EXECUTABLE - Filename of the rustc executable. You can fill it in using the Findrustc package.
# RUSTDOC_EXECUTABLE - Filename of the rustc executable. You can fill it in using the Findrustdoc package.
# RUSTC_FLAGS - These get passed to rustc.
# RUSTDOC_FLAGS - These flags get passed to rustdoc.

if(NOT RUSTC_FLAGS)
	set(RUSTC_FLAGS "" CACHE STRING "Flags to pass to the Rust compiler.")
endif()
mark_as_advanced(RUSTC_FLAGS)

if(NOT RUSTDOC_FLAGS)
	set(RUSTDOC_FLAGS "" CACHE STRING "Flags to pass to Rustdoc.")
endif()
mark_as_advanced(RUSTDOC_FLAGS)

# Fetches the dependencies of a Rust crate with local crate root located at
# local_root_file and places them into out_var.
#
# Additional arguments get passed to rustc.
#
# NOTE: Only the first target's dependencies are fetched!
function(get_rust_deps local_root_file out_var)
	set(root_file "${CMAKE_SOURCE_DIR}/${local_root_file}")

	set(dep_dir "${CMAKE_BINARY_DIR}/CMakeFiles/.cmake_rust_dependencies")
	file(MAKE_DIRECTORY "${dep_dir}")
	
	message(STATUS "Getting Rust dependency info for crate root ${root_file}")
	
	execute_process(COMMAND ${RUSTC_EXECUTABLE} ${RUSTC_FLAGS} ${ARGN} --no-analysis --dep-info "${dep_dir}/deps" "${root_file}")
	
	# Read and parse the dependency information
	file(READ "${dep_dir}/deps" crate_deps)
	file(REMOVE "${dep_dir}/deps")
	string(REGEX REPLACE ".*:(.*)" "\\1" crate_deps "${crate_deps}")
	string(STRIP "${crate_deps}" crate_deps)
	string(REPLACE " " ";" crate_deps "${crate_deps}")
	
	# Make the dependencies be relative to the source directory
	set(crate_deps_relative "")
	foreach(var IN ITEMS ${crate_deps})
		file(RELATIVE_PATH var "${CMAKE_CURRENT_SOURCE_DIR}" "${var}")
		list(APPEND crate_deps_relative "${var}")
		
		# Hack to re-run CMake if the file changes
		configure_file("${var}" "${dep_dir}/${var}" COPYONLY)
	endforeach()
	
	set(${out_var} "${crate_deps_relative}" PARENT_SCOPE)
endfunction()

# Adds a target to build a rust crate with the crate root located at local_root_file.
# The compilation output is placed inside target_dir within the build directory.
#
# Please pass the crate dependencies obtained through get_rust_deps to the dependencies argument in 
# addition to other targets/files you want this target to depend on.
#
# This macro sets two variables:
#
# ${target_name}_FULL_TARGET - This is what you would use as a set of dependencies
#                              for other targets when you want them to depend on the
#                              output of this target.
#
# ${target_name}_ARTIFACTS -   The actual files generated by this command. 
#                              You'd use this as a set of files to install/copy elsewhere.
#
# Additional arguments get passed to rustc.
macro(rust_crate target_name local_root_file target_dir dependencies)
	set(root_file "${CMAKE_SOURCE_DIR}/${local_root_file}")
	
	execute_process(COMMAND ${RUSTC_EXECUTABLE} ${RUSTC_FLAGS} ${ARGN} --crate-file-name "${root_file}"
	                OUTPUT_VARIABLE crate_filename
	                OUTPUT_STRIP_TRAILING_WHITESPACE)
	
	set(comment "Building ${target_dir}/${crate_filename}")
	set(crate_filename "${CMAKE_BINARY_DIR}/${target_dir}/${crate_filename}")
	file(MAKE_DIRECTORY "${CMAKE_BINARY_DIR}/${target_dir}")
	
	add_custom_command(OUTPUT "${crate_filename}"
	                   COMMAND ${RUSTC_EXECUTABLE} ${RUSTC_FLAGS} ${ARGN} --out-dir "${CMAKE_BINARY_DIR}/${target_dir}" "${root_file}"
	                   DEPENDS ${crate_deps_list}
	                   DEPENDS ${dependencies}
	                   COMMENT "${comment}")

	add_custom_target("${target_name}"
	                  DEPENDS "${crate_filename}")
	
	set("${target_name}_ARTIFACTS" "${crate_filename}")
	# CMake Bug #10082
	set("${target_name}_FULL_TARGET" "${target_name};${crate_filename}")
endmacro(rust_crate)

# Like rust_crate, but fetches the crate dependencies for you. This is convenient
# but inefficient if you want to compile the crate several times with different
# configurations (e.g. with --test and without) or if you want to build documentation.
macro(rust_crate_auto target_name local_root_file target_dir other_dependencies)
	get_rust_deps(${local_root_file} crate_deps_list ${ARGN})
	
	rust_crate("${target_name}" "${local_root_file}" "${target_dir}" "${crate_deps_list};${other_dependencies}" ${ARGN})
endmacro(rust_crate_auto)

# Like rust_crate, but this time it generates documentation using rust_doc.
# 
# Additional arguments get passed to rustdoc.
macro(rust_doc target_name local_root_file target_dir dependencies)
	set(root_file "${CMAKE_SOURCE_DIR}/${local_root_file}")
	
	execute_process(COMMAND ${RUSTC_EXECUTABLE} ${RUSTC_FLAGS} ${ARGN} --crate-name "${root_file}"
	                OUTPUT_VARIABLE crate_name
	                OUTPUT_STRIP_TRAILING_WHITESPACE)
	
	set(doc_dir "${CMAKE_BINARY_DIR}/${target_dir}/${crate_name}")
	set(src_dir "${CMAKE_BINARY_DIR}/${target_dir}/src/${crate_name}")
	file(MAKE_DIRECTORY "${CMAKE_BINARY_DIR}/${target_dir}")
	
	add_custom_command(OUTPUT "${doc_dir}" "${src_dir}"
	                   COMMAND ${RUSTDOC_EXECUTABLE} ${RUSTDOC_FLAGS} ${ARGN} -o "${CMAKE_BINARY_DIR}/${target_dir}" "${root_file}"
	                   # Otherwise CMake will never stop remaking the documentation after a change that doesn't change the output
	                   COMMAND ${CMAKE_COMMAND} -E touch "${doc_dir}"
	                   COMMAND ${CMAKE_COMMAND} -E touch "${src_dir}"
	                   DEPENDS ${crate_deps_list}
	                   DEPENDS ${dependencies})

	add_custom_target("${target_name}"
	                  DEPENDS "${doc_dir}"
	                  DEPENDS "${src_dir}")
	
	set("${target_name}_ARTIFACTS" "${doc_dir};${src_dir}")
	# CMake Bug #10082
	set("${target_name}_FULL_TARGET" "${target_name};${doc_dir};${src_dir}")
endmacro(rust_doc)

# Like rust_crate_auto, but this time it generates documentation using rust_doc.
macro(rust_doc_auto target_name local_root_file target_dir other_dependencies)
	get_rust_deps(${local_root_file} crate_deps_list ${ARGN})
	
	rust_doc("${target_name}" "${local_root_file}" "${target_dir}" "${crate_deps_list};${other_dependencies}" ${ARGN})
endmacro(rust_doc_auto)
